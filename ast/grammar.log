(Game | Stage |PlayerName | TeamName | Location | Token) → ID

(Precedence | Combo | Memory | PointMap | Key | Value) → ID

Player →   PlayerName
         | 'current'
         | 'next'
         | 'previous'
         | 'competitor'
         | 'Turnorder' Int
         | 'owner' 'of' CardPosition
         | 'owner' 'of' ('highest' | 'lowest') [Memory]

Int →   INT
      | '(' Int ('+' | '-' | '*' | '//' | 'mod') Int ')'
      | [IntCollection] Int
      | size' 'of' [Collection]
      | 'sum' 'of' ([IntCollection] | CardSet 'using' [PointMap])
      | ('min' | 'max') 'of' [IntCollection]
      | 'stageroundcounter'
      | 'playroundcounter'

String →  ID
        | [Key] 'of' Location (Int | 'Top' | 'Bottom')
        | [Key] 'of' ('min' | 'max') 'of' CardSet 'using' Precedence
        | [Key] 'of' ('min' | 'max') 'of' CardSet 'using' PointMap
        | [StringCollection] Int

CardPosition →   Location (Int | 'Top' | 'Bottom')
               | ('min' | 'max') 'of' CardSet 'using' [Precedence]
               | ('min' | 'max') 'of' CardSet 'using' [PointMap]

Bool →   String ('==' | '!=') String
       | Int ('==' | '!=' | '<' | '>' | '<=' | '>=') Int
       | CardSet ('==' | '!=') CardSet
       | CardSet 'is' ('not')? 'empty'
       | Player ('==' | '!=') Player
       | Team ('==' | '!=') Team
       | '(' Bool ('and' | 'or') Bool ')'
       | 'not' '(' Boolean ')'
       | [Player]         'out' 'of' ([Stage] | 'stage' | 'play' | 'game')
       | PlayerCollection 'out' 'of' ([Stage] | 'stage' | 'play' | 'game')

Status →    'faceup'
          | 'facedown'
          | 'private'

Team →   TeamName
       | 'team' 'of' [Player]

Collection →   IntCollection
             | StringCollection
             | PlayerCollection
             | TeamCollection
             | LocationCollection
             | CardSet

IntCollection → '(' Int (',' Int)+ ')'

StringCollection → '(' String (',' String)+ ')'

PlayerCollection →    '(' [Player] (',' [Player])+ ')'
                    | 'others'
                    | Quantifier
                    | 'playersout'
                    | 'playersin'

TeamCollection →   '(' [Team] (',' [Team])+ ')'
                 | 'other' 'teams'

LocationCollection → '(' [Location] (',' [Location])+ ')'

Game → FlowComponent+

FlowComponent → SeqStage | Rule  

EndCondition →    'until' Bool
                | 'until' Bool 'and' Repetitions
                | 'until' Bool 'or' Repetitions
                | Repetitions
                | 'until' 'end'

Repetitions →   Int 'times'

CreatePlayer → 'Player' PlayerName (',' PlayerName)*

CreateTeam → 'Team' TeamName 'with' PlayerCollection (',' TeamName 'with' PlayerCollection)*

CreateTurnorder →   'Turnorder' PlayerCollection ('random')?
                  | 'Turnorder' PlayerCollection ('random')?

CreateLoc →   'Location' Location (',' Location)* 'on' PlayerCollection
            | 'Location' Location (',' Location)* 'on' TeamCollection
            | 'Location' Location (',' Location)* 'on' 'table'

CreateCard → 'Card' 'on' [Location] ':' Key Value (',' Value)* ('for' Key Value (',' Value)*)*

CreateToken → 'Token' INT Token 'on' [Location]

CreatePrec →   'Precedence' Precedence 'on' [Key] '(' [Value] (',' [Value])* ')'
             | 'Precedence' Precedence '(' [Key] [Value] (',' [Key] [Value])* ')'

CreateCombo → 'Combo' Combo 'where' Filter

CreateMem →   'Memory' Memory Int 'on' PlayerCollection
            | 'Memory' Memory String 'on' PlayerCollection
            | 'Memory' Collection 'on' PlayerCollection
            | 'Memory' Memory Int 'on' 'table'
            | 'Memory' Memory String 'on' 'table'
            | 'Memory' Collection 'on' 'table'

CreatePointMap →   'Points' PointMap 'on' [Key] '(' [Val] ':' Int (',' [Val] ':' Int)* ')'
                  |'Points' PointMap '(' [Key] [Val] ':' Int (',' [Key] [Val] ':' Int)* ')'

Stage → SimStage | SeqStage

SimStage → 'Stage' Stage 'for' PlayerCollection EndCondition ':' ('create' SetupRule |
PlayRule | ScoringRule)+ '}'

SeqStage → 'Stage' Stage 'for' [Player] EndCondition '{' FlowComponent+  '}'

CondRule → 'conditional:' ('case' Bool? ':' Rule+)+ ']'' | IfRule

IfRule → 'if' '(' Bool ')' '{' FlowComponent+ '}'

OptionalRule → 'optional:' FlowComponent+

ChoiceRule → 'choose' '{' FlowComponent+ (',' FlowComponent+)* '}'

FlipAction → 'flip' CardSet 'to' Status

ShuffleAction → 'shuffle' CardSet

OutAction →   'set' [Player]         'out' 'of' ( 'stage' | 'play' | 'game' ('successful' | 'fail'))
            | 'set' PlayerCollection 'out' 'of' ( 'stage' | 'play' | 'game' ('successful' | 'fail'))

SetMem → Memory 'is' (Int | String | Collection)

ResetMem → 'reset' [Memory]

CycleAction → 'cycle' 'to' [Player]

BidAction →   'bid' Quantity
            | 'bid' Quantity 'on' Memory

EndAction →   'end' ('turn' | 'stage' | 'play' | 'game' 'with' 'winner' [Player])
            | 'end' ('turn' | 'stage' | 'play' | 'game' 'with' 'winner' PlayerCollection)

DemAction →   'demand' (CardPosition | String | Int)
            | 'demand' (CardPosition | String | Int) 'as' Memory

Quantity →   Int
           | Quantifier
           | IntRange

IntRangeHelper → ('==' | '!=' | '<' | '>' | '<=' | '>=') Int

IntRange → IntRangeHelper (('and' | 'or') IntRangeHelper)*

Quantifier →   'all'
             | 'any'

CardSet →   Group
          | Group 'of' [Player]
          | Group 'of' PlayerCollection

Group →   [Location]
        | [Location] 'where' Filter
        | LocationCollection
        | LocationCollection 'where' Filter
        | ('not')? [Combo] 'in' [Location]
        | ('not')? [Combo] 'in' LocationCollection
        | CardPosition

Filter →   [Key] 'same'
         | [Key] 'distinct' 
         | [Key] 'adjacent' 'using' [Precedence]
         | [Key] 'higher' 'using' [Precedence]
         | [Key] 'lower' 'using' [Precedence]
         | 'size' '==' Int
         | 'size' '!=' Int
         | 'size' '<'  Int
         | 'size' '>'  Int
         | 'size' '<=' Int
         | 'size' '>=' Int
         | [Key] ('==' | '!=') String |
         | ('not')? [Combo]
         | '(' Filter ('and' | 'or') Filter ')'

ClassicMove →   'move'                 CardSet Status 'to' CardSet
              | 'move' Quantity 'from' CardSet Status 'to' CardSet

DealMove →    'deal'                 CardSet Status 'to' CardSet
            | 'deal' Quantity 'from' CardSet Status 'to' CardSet

ExchangeMove →    'exchange'                 CardSet Status 'to' CardSet
                | 'exchange' Quantity 'from' CardSet Status 'to' CardSet

TokenMove →   'place'                         TokenLoc 'to' TokenLoc
            | 'place' Quantity [Token] 'from' TokenLoc 'to' TokenLoc

TokenLoc →   [Location]         'of' [Player]
           | LocationCollection 'of' [Player]
           | [Location]         'of' PlayerCollection
           | LocationCollection 'of' PlayerCollection

ScoreRule →   'score' Int               'of' [PlayerName]
            | 'score' Int 'to' [Memory] 'of' [PlayerName]
            | 'score' Int               'of' PlayerCollection
            | 'score' Int 'to' [Memory] 'of' PlayerCollection


WinnerRule →   'winner' 'is' [PlayerName]
             | 'winner' 'is' PlayerCollection
             | 'winner' 'is' ('lowest' | 'highest') 'Score'
             | 'winner' 'is' ('lowest' | 'highest') 'Position'
             | 'winner' 'is' ('lowest' | 'highest') [Memory]
